#!/hint/bash

_pkgname="waifu2x-ncnn-vulkan"
pkgname="$_pkgname-git"
pkgver=20250802.r3.g567ba9c
pkgrel=2
pkgdesc="NCNN Implementation of Waifu2x Converter"
url="https://github.com/nihui/waifu2x-ncnn-vulkan"
arch=('x86_64')
license=('MIT')

depends=(
    'gcc-libs'
    'ncnn'
    'libjpeg-turbo'
    # 'zlib-ng-compat'  conflicts with zlib
    'libpng'
    'libwebp'
    'vulkan-icd-loader'
)
makedepends=(
    'cmake'
    'git'
    'glslang'
    'ninja'
    'vulkan-headers'
    'openmp'
)

provides=("$_pkgname")
conflicts=("$_pkgname")

source=(
    "$_pkgname"::"git+$url.git"
)
sha256sums=(
    'SKIP'
)
options=('!debug')

pkgver() {
    cd "$_pkgname"
    git describe --long --tags --abbrev=7 \
        | sed -E 's/^[^0-9]*//;s/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
    cd "$_pkgname"

    # Update submodules
    git submodule update --init --recursive --recommend-shallow

    # Fix default model path
    sed -i 's|path_t model = PATHSTR("models-cunet")|path_t model = PATHSTR("/usr/share/'"$_pkgname"'/models-cunet")|' src/main.cpp
}

build() {
    local _cmake_options=(
        -B build
        -S "$_pkgname/src"
        -G Ninja
        -DCMAKE_BUILD_TYPE=Release
        -DBUILD_SHARED_LIBS='OFF'
        -DCMAKE_INSTALL_PREFIX='/usr'
        -DUSE_SYSTEM_NCNN='ON'
        -DUSE_SYSTEM_JPEG='ON'
        -DUSE_SYSTEM_ZLIB='ON'
        -DUSE_SYSTEM_PNG='ON'
        -DUSE_SYSTEM_WEBP='ON'
        -Wno-dev
    )

    cmake "${_cmake_options[@]}"
    cmake --build build
}

package() {
    install -Dm755 build/$_pkgname -t "$pkgdir/usr/share/$_pkgname/"
    install -d "$pkgdir/usr/bin"
    ln -s /usr/share/$_pkgname/$_pkgname "$pkgdir/usr/bin/$_pkgname"

    install -Dm644 "$_pkgname/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"

    cd "$_pkgname/models"
    for f in models-*/*; do
        install -Dm644 "$f" "$pkgdir/usr/share/$_pkgname/$f"
    done
}


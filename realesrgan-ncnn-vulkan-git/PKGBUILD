#!/hint/bash

_pkgdir="Real-ESRGAN-ncnn-vulkan"
_pkgname="realesrgan-ncnn-vulkan"
pkgname="$_pkgname-git"
pkgver=0.2.0.r0.g37026f4
pkgrel=2
pkgdesc='NCNN implementation of Real-ESRGAN'
arch=('x86_64')
url='https://github.com/xinntao/Real-ESRGAN-ncnn-vulkan'
license=('MIT')
depends=(
    'gcc-libs'
    'libwebp'
    'ncnn'
    'vulkan-icd-loader'
    'vulkan-driver'
)
makedepends=(
    'cmake'
    'git'
    'glslang'
    'ninja'
    'vulkan-headers'
    'openmp'
)

provides=("$_pkgname")
conflicts=("$_pkgname")

source=(
    "$_pkgdir"::"git+https://github.com/rokibhasansagar/Real-ESRGAN-ncnn-vulkan-mirror.git#branch=vulkan-ci-update"
    'https://github.com/xinntao/Real-ESRGAN/releases/download/v0.2.5.0/realesrgan-ncnn-vulkan-20220424-ubuntu.zip'
)
b2sums=('SKIP'
        'c16c05c343501272f5b4df6db7e7f8c592c4f8c8170ca0a0181c4433d1889ddea702d65fc134013bdaca302eeeab6e5b6d6fbad407adb7a2afa0acbefd6f4eca')
options=('!debug')

pkgver() {
    echo "0.2.0.r0.g37026f4"
    # cd "$_pkgdir"
    # git describe --long --tags \
    #     | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "$_pkgdir"

    # Update STB headers
    for _h in stb_image{,_write}.h; do
        curl -sL "https://github.com/nothings/stb/raw/master/$_h" -o src/$_h
    done

    # Fix default model path
    sed -i 's|path_t model = PATHSTR("models")|path_t model = PATHSTR("/usr/share/'"$_pkgname"'/models")|' src/main.cpp
}

build() {

    local _cmake_options=(
        -B build
        -S "$_pkgdir/src"
        -G Ninja
        -DCMAKE_BUILD_TYPE=Release
        -DBUILD_SHARED_LIBS='OFF'
        -DCMAKE_INSTALL_PREFIX='/usr'
        -DUSE_SYSTEM_NCNN='ON'
        -DUSE_SYSTEM_WEBP='ON'
        -Wno-dev
    )

    cmake "${_cmake_options[@]}"
    cmake --build build
}

package() {
    install -Dm755 build/$_pkgname -t "$pkgdir/usr/share/$_pkgname/"
    install -d "$pkgdir/usr/bin"
    ln -s /usr/share/$_pkgname/$_pkgname "$pkgdir/usr/bin/$_pkgname"

    install -Dm644 "$_pkgdir/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"

    # cd "$_pkgname/models"
    for f in models/*; do
        install -Dm644 "$f" "$pkgdir/usr/share/$_pkgname/$f"
    done
}


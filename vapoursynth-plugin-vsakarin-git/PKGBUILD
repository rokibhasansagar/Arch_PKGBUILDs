#!/hint/bash

_plug=vsakarin
pkgname=vapoursynth-plugin-${_plug}-git
pkgver=0.96f.r132.1e0d846
pkgrel=2
pkgdesc="Plugin for Vapoursynth: ${_plug} (GIT version)"
arch=('x86_64')
url='https://github.com/akarinVS/vapoursynth-plugin.git'
license=('GPL')
depends=(
    'vapoursynth'
    'gcc-libs' # libgcc_s.so libstdc++.so
    'glibc' # ld-linux-x86-64.so libc.so libm.so
    'llvm-libs' # libLLVM.so
)
makedepends=(
    'git'
    'meson'
    'llvm'
)
provides=("vapoursynth-plugin-${_plug}")
conflicts=("vapoursynth-plugin-${_plug}")
source=(
    "${_plug}::git+https://github.com/rokibhasansagar/vapoursynth-plugin-vsakarin.git#branch=llvm19"
)
sha256sums=(
    'SKIP'
)
options=('!debug')

pkgver() {
    cd "${_plug}"
    printf "0.96f.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    arch-meson "${_plug}" build \
        --buildtype=release \
        -Dstatic-llvm=false

    meson compile -C build
}

package() {
    DESTDIR="${pkgdir}" meson install -C build

    install -Dm644 "${_plug}/README.md" "${pkgdir}/usr/share/doc/vapoursynth/plugins/${_plug}/README.md"
}

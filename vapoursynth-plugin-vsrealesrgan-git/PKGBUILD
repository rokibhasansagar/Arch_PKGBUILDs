#!/hint/bash

_plug=vsrealesrgan
pkgname=vapoursynth-plugin-${_plug}-git
pkgver=4.0.1.2.gba2fe5c
pkgrel=2
pkgdesc="Plugin for Vapoursynth: ${_plug} (GIT version)"
arch=('any')
url='https://github.com/HolyWu/vs-realesrgan'
license=('BSD')
depends=(
    'vapoursynth'
    'python-numpy'
)
makedepends=(
    'git'
    'python-pip'
    'python-tqdm'
    'python-requests'
)
optdepends=(
    'python-pytorch: CPU with AVX2 optimizations'
    'python-pytorch-cuda: CUDA with CPU with AVX2 optimizations'
)
provides=("vapoursynth-plugin-${_plug}")
conflicts=("vapoursynth-plugin-${_plug}")
source=("${_plug}::git+https://github.com/HolyWu/vs-realesrgan.git")
sha256sums=('SKIP')

pkgver() {
    git -C "${_plug}" describe --long --tags | tr - . | tr -d v
}

# models need to be fetched in ${_plug}/models, makedepends+=('python-tqdm' 'python-requests')
prepare() {
    cd "${_plug}/vsrealesrgan"
    python __main__.py
}

build() {
    cd "${_plug}"
    pip wheel --no-deps . -w dist
}

package() {
    cd "${_plug}"
    pip install -I -U --root "${pkgdir}" --no-warn-script-location --no-deps dist/*.whl

    install -Dm644 README.md "${pkgdir}/usr/share/doc/vapoursynth/plugins/${_plug}/README.md"
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

